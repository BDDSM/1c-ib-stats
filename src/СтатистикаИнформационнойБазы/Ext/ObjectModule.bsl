
#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПользовательскиеНастройкиМодифицированы = Ложь;
	
	// Подготовим и выведем отчет.
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	
	НастройкиКомпоновкиДанных = КомпоновщикНастроек.ПолучитьНастройки();
		
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиКомпоновкиДанных, ДанныеРасшифровки);
		
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	
	ТаблицаСтатистики = СтатистикаПоОбъектам();
	
	ВнешниеНаборыДанных = Новый Структура("СтатистикаИБ", ТаблицаСтатистики);
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, ВнешниеНаборыДанных, ДанныеРасшифровки, Истина);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	
	ПроцессорВывода.НачатьВывод();
	ПроцессорВывода.Вывести(ПроцессорКомпоновки, Истина);
	ПроцессорВывода.ЗакончитьВывод();
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СтатистикаПоОбъектам()
	
	ТаблицаСтатистики = ПустаяТаблицаСтатистики();
	
	Индекс = 1;
	
	Для Каждого Тип Из ТипыМетаданных() Цикл
		
		// Примеры: 
		// 01. Справочники
		// 10. БизнесПроцессы
		ПредставлениеТипа = Прав("0" + Строка(Индекс), 2) + ". " + Тип;
		
		Для Каждого Объект Из ОбъектыТипа(Тип) Цикл
			Строка = ТаблицаСтатистики.Добавить();
			Строка.Тип = ПредставлениеТипа;
			Строка.Наименование = Объект.Имя;
			Строка.Количество = КоличествоОбъектов(Объект);
		КонецЦикла;
		
		Индекс = Индекс + 1;
		
	КонецЦикла;
	
	Возврат ТаблицаСтатистики;
	
КонецФункции

Функция ОбъектыТипа(Тип)
	
	Возврат Метаданные[Тип];
	
КонецФункции

Функция КоличествоОбъектов(Объект)
	
	Запрос = Новый Запрос("ВЫБРАТЬ КОЛИЧЕСТВО(1) КАК Количество ИЗ " + Объект.ПолноеИмя());
	
	Результат = Запрос.Выполнить().Выбрать();
	Результат.Следующий();
	
	Возврат Результат.Количество;
	
КонецФункции

Функция ПустаяТаблицаСтатистики()
	
	ТЗ = Новый ТаблицаЗначений;
	ТЗ.Колонки.Добавить("Тип", ОписаниеСтроки(50));
	ТЗ.Колонки.Добавить("Наименование", ОписаниеСтроки(250));
	ТЗ.Колонки.Добавить("Количество", ОписаниеЧисла(10));
	
	Возврат ТЗ;
	
КонецФункции

Функция ОписаниеСтроки(Длина)
	Возврат Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(Длина));
КонецФункции

Функция ОписаниеЧисла(Разряды)
	Возврат Новый ОписаниеТипов("Число", , , Новый КвалификаторыЧисла(Разряды));
КонецФункции

Функция ТипыМетаданных()
	
	ТипыМетаданных = Новый Массив;
	ТипыМетаданных.Добавить("Справочники");
	ТипыМетаданных.Добавить("Документы");
	ТипыМетаданных.Добавить("ПланыВидовХарактеристик");
	ТипыМетаданных.Добавить("ПланыСчетов");
	ТипыМетаданных.Добавить("ПланыВидовРасчета");
	ТипыМетаданных.Добавить("РегистрыСведений");
	ТипыМетаданных.Добавить("РегистрыНакопления");
	ТипыМетаданных.Добавить("РегистрыБухгалтерии");
	ТипыМетаданных.Добавить("РегистрыРасчета");
	ТипыМетаданных.Добавить("БизнесПроцессы");
	ТипыМетаданных.Добавить("Задачи");

	Возврат ТипыМетаданных;
	
КонецФункции

#КонецОбласти